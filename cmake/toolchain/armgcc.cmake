CMAKE_MINIMUM_REQUIRED (VERSION 3.18)

# TOOLCHAIN EXTENSION
IF(WIN32)
    SET(TOOLCHAIN_EXT ".exe")
ELSE()
    SET(TOOLCHAIN_EXT "")
ENDIF()

# EXECUTABLE EXTENSION
SET (CMAKE_EXECUTABLE_SUFFIX ".elf")

# TOOLCHAIN_DIR AND NANO LIBRARY
SET(TOOLCHAIN_DIR $ENV{ARMGCC_DIR})
STRING(REGEX REPLACE "\\\\" "/" TOOLCHAIN_DIR "${TOOLCHAIN_DIR}")

# TARGET_TRIPLET
SET(TARGET_TRIPLET "arm-none-eabi")

IF(NOT TOOLCHAIN_DIR)
    # MESSAGE(FATAL_ERROR "***Please set ARMGCC_DIR in envionment variables***")
    # https://developer.arm.com/downloads/-/arm-gnu-toolchain-downloads

    if(NOT CMAKE_HOST_SYSTEM_PROCESSOR)
        message(FATAL_ERROR "CMAKE_HOST_SYSTEM_PROCESSOR unknown")
    endif()

    set(TOOLCHAINS_STORAGE_DIR "${CMAKE_SOURCE_DIR}/toolchain")
    set(ARM_TOOLCHAIN_NAME "arm-gnu-toolchain-14.2.rel1-${CMAKE_HOST_SYSTEM_PROCESSOR}-${TARGET_TRIPLET}")
    if (${CMAKE_HOST_SYSTEM_PROCESSOR} STREQUAL "x86_64")
        set(ARM_TOOLCHAIN_HASH)
    elseif(${CMAKE_HOST_SYSTEM_PROCESSOR} STREQUAL "aarch64")
        set(ARM_TOOLCHAIN_HASH)
    else()
        message(FATAL_ERROR "Can't download ARM toolchain for host architecture: ${CMAKE_HOST_SYSTEM_PROCESSOR}")
    endif()

    set(ARM_TOOLCHAIN_URL "https://developer.arm.com/-/media/Files/downloads/gnu/14.2.rel1/binrel/${ARM_TOOLCHAIN_NAME}.tar.xz")
    if (NOT EXISTS ${TOOLCHAINS_STORAGE_DIR}/${ARM_TOOLCHAIN_NAME}.tar.xz)
        message(STATUS "Downloading Arm GCC toolchain ${ARM_TOOLCHAIN_URL} to: ${TOOLCHAINS_STORAGE_DIR}")
        file(DOWNLOAD ${ARM_TOOLCHAIN_URL} ${TOOLCHAINS_STORAGE_DIR}/${ARM_TOOLCHAIN_NAME}.tar.xz
            # EXPECTED_HASH SHA256=62a63b981fe391a9cbad7ef51b17e49aeaa3e7b0d029b36ca1e9c3b2a9b78823
            SHOW_PROGRESS)
    endif()
    if(NOT EXISTS ${TOOLCHAINS_STORAGE_DIR}/${ARM_TOOLCHAIN_NAME}) # prevent repeated extractions
        file(ARCHIVE_EXTRACT INPUT ${TOOLCHAINS_STORAGE_DIR}/${ARM_TOOLCHAIN_NAME}.tar.xz
            DESTINATION ${TOOLCHAINS_STORAGE_DIR})
    endif()
    set(TOOLCHAIN_DIR "${TOOLCHAINS_STORAGE_DIR}/${ARM_TOOLCHAIN_NAME}")
ENDIF()

MESSAGE(STATUS "TOOLCHAIN_DIR: " ${TOOLCHAIN_DIR})

SET(TOOLCHAIN_BIN_DIR ${TOOLCHAIN_DIR}/bin)
SET(TOOLCHAIN_INC_DIR ${TOOLCHAIN_DIR}/${TARGET_TRIPLET}/include)
SET(TOOLCHAIN_LIB_DIR ${TOOLCHAIN_DIR}/${TARGET_TRIPLET}/lib)

SET(CMAKE_SYSTEM_NAME Generic)
SET(CMAKE_SYSTEM_PROCESSOR arm)

SET(CMAKE_C_COMPILER ${TOOLCHAIN_BIN_DIR}/${TARGET_TRIPLET}-gcc${TOOLCHAIN_EXT})
SET(CMAKE_CXX_COMPILER ${TOOLCHAIN_BIN_DIR}/${TARGET_TRIPLET}-g++${TOOLCHAIN_EXT})
set(CMAKE_C_COMPILER_WORKS ON)
set(CMAKE_CXX_COMPILER_WORKS ON)
SET(CMAKE_ASM_COMPILER ${TOOLCHAIN_BIN_DIR}/${TARGET_TRIPLET}-gcc${TOOLCHAIN_EXT})

SET(CMAKE_OBJCOPY ${TOOLCHAIN_BIN_DIR}/${TARGET_TRIPLET}-objcopy CACHE INTERNAL "objcopy tool")
SET(CMAKE_OBJDUMP ${TOOLCHAIN_BIN_DIR}/${TARGET_TRIPLET}-objdump CACHE INTERNAL "objdump tool")

# SET(CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} -O0 -g" CACHE INTERNAL "c compiler flags debug")
# SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -O0 -g" CACHE INTERNAL "cxx compiler flags debug")
# SET(CMAKE_ASM_FLAGS_DEBUG "${CMAKE_ASM_FLAGS_DEBUG} -g" CACHE INTERNAL "asm compiler flags debug")
# SET(CMAKE_EXE_LINKER_FLAGS_DEBUG "${CMAKE_EXE_LINKER_FLAGS_DEBUG}" CACHE INTERNAL "linker flags debug")

# SET(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} -O3 " CACHE INTERNAL "c compiler flags release")
# SET(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -O3 " CACHE INTERNAL "cxx compiler flags release")
# SET(CMAKE_ASM_FLAGS_RELEASE "${CMAKE_ASM_FLAGS_RELEASE}" CACHE INTERNAL "asm compiler flags release")
# SET(CMAKE_EXE_LINKER_FLAGS_RELESE "${CMAKE_EXE_LINKER_FLAGS_RELESE}" CACHE INTERNAL "linker flags release")

SET(CMAKE_FIND_ROOT_PATH ${TOOLCHAIN_DIR}/${TARGET_TRIPLET} ${EXTRA_FIND_PATH})
SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
